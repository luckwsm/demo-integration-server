buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.1'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = 1.8
targetCompatibility = 1.8

if (!JavaVersion.current().java8Compatible) {
    throw new IllegalStateException('''A Haiku:
                                      |  This needs Java 8,
                                      |  You are using something else,
                                      |  Refresh. Try again.'''.stripMargin())
}

repositories {
    mavenCentral()
    maven {
        url = 'http://oss.sonatype.org/content/repositories/snapshots/'
    }
    flatDir {
        dirs 'lib'
    }
}
dependencies {
    compile 'com.google.guava:guava:20.0-SNAPSHOT'

    compile 'org.apache.commons:commons-lang3:3.3.2'
    compile 'commons-codec:commons-codec:1.9'

    compile "io.vertx:vertx-core:$vertxVersion"
    compile "io.vertx:vertx-web:$vertxVersion"

    compile 'com.typesafe:config:1.3.0'

    compile "org.bitbucket.b_c:jose4j:0.4.4"


    testCompile "io.vertx:vertx-unit:$vertxVersion"
    testCompile 'junit:junit:4.12'
}

test {
    environment 'PORT', '8080'
    environment 'DEFAULT_VENDOR_ID', 'demo'
    environment 'DEFAULT_AUTH_TOKEN', 'eyJraWQiOiJkZW1vQVBJTHlyaWMiLCJhbGciOiJSUzI1NiJ9.eyJpc3MiOiJMeXJpYyIsImF1ZCI6Ikx5cmljIiwiZXhwIjo0NjEzNDU4MzUyLCJqdGkiOiJBaXcyNS1NNUcyTzhRUUxIUGg0YXN3IiwiaWF0IjoxNDU5ODU4MzUyLCJuYmYiOjE0NTk4NTgyMzIsInN1YiI6ImRlbW8iLCJ2ZW5kb3JJZCI6ImRlbW8ifQ.giKyADgbnbhqUqRDhP6LpdNW_hk7g0pxt0bZ3kn4rwxNfkOQ-1rWsIE_tWSLfBAniEpfiQlQzArgT2Lu_kDQU18NHd9334yxKmfQIpMifowRUXEB6TxLpxU-8yirLNLwV5kWwZUpl0io6JDydhSuRYD1xiAiUdv176OZuKleRuohf36QPg3NAhkU3RwdgkWxmFoKmFs3FXkw0eWk49tvbvmM4ylHBKteo8IW-_oyeW5xiyR-nEoaQguhmmZ3_ARw4qKqzNBkDWKTmHQQAuocudSy_UJLFOF7kFhjLpphyJLwL7m5Rc_KYVfN7NgkBLxkPGrsq45OTOyFwe2cDe-qJQ'
    environment 'DEFAULT_INTEGRATION_SERVICES_HOST', 'devservices.lyricfinancial.com'
}

shadowJar {
    baseName = "$packageName"
    version =  "$packageVersion"
    classifier = 'fat'
    manifest {
        attributes 'Main-Class': 'io.vertx.core.Starter'
        attributes 'Main-Verticle': 'com.lyric.Start'
    }
    mergeServiceFiles {
        include 'META-INF/services/io.vertx.core.spi.VerticleFactory'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.2'
}

task stage {
    dependsOn shadowJar
}
